# Not needed as of 2023-08-16. Keeping in case Intel breaks it again.
# Fixed PKGBUILD from https://github.com/stefanpartheym/archlinux-ipu6-webcam
# Uses earlier version of icamerasrc. See https://github.com/intel/icamerasrc/pull/31#discussion_r1184456155
# Original PKGBUILD: https://aur.archlinux.org/packages/icamerasrc-git
_pkgname=icamerasrc
pkgname=icamerasrc-git-fix
pkgver=r65.931412a
pkgrel=1
pkgdesc="Intel IPU6 camera source for GStreamer"
arch=('x86_64')
url="https://github.com/intel/icamerasrc"
license=('LGPL2.1')
depends=('ipu6-camera-hal'
         'gstreamer'
         'libdrm'
         'gst-plugins-base')
makedepends=('git'
             'autoconf'
             'make'
             'gcc')
conflicts=('icamerasrc-git')
source=("git+${url}.git#commit=931412a66cdf5d24ed77aa3b652a958dae9b2e9e")
sha256sums=('SKIP')

prepare() {
    cd $_pkgname
    sed -i "s|-D_FORTIFY_SOURCE=2| |" src/Makefile.am
    sed -i "s|-D_FORTIFY_SOURCE=2| |" src/interfaces/Makefile.am
}

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_pkgname"
    export CHROME_SLIM_CAMHAL=ON
    export STRIP_VIRTUAL_CHANNEL_CAMHAL=ON
    ./autogen.sh
    make
}

package() {
    cd "$srcdir/$_pkgname"
    mkdir -p $pkgdir/usr/lib/udev/rules.d
    printf '%s\n%s\n%s\n' \
           '# This rule makes the Intel IPU6 sensor accessible to members of group "video",\n' \
           '# allowing icamerasrc to be used as an unprivileged user.\n' \
           'SUBSYSTEM=="intel-ipu6-psys",GROUP="video",MODE="0660"' \
           > $pkgdir/usr/lib/udev/rules.d/70-ipu6-psys.rules
    make DESTDIR="$pkgdir" install
}
